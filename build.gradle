subprojects {
  apply plugin: 'java'
  apply plugin: 'maven-publish'

  group 'me.yushust.message'
  version '6.0.7'

  publishing {
    repositories {
      maven {
        def snapshot = version.toString().endsWith("-SNAPSHOT")
        def typeName = snapshot ? 'snapshots' : 'releases'
        def repoUrl = 'https://repo.unnamed.team/repository/unnamed-' + typeName

        name = snapshot ? 'unnamed-snapshots' : 'unnamed-releases'
        url = repoUrl
        credentials {
          username = System.getenv('REPO_USER')
          password = System.getenv('REPO_PASSWORD')
        }
      }
    }
    publications {
      nmessage(MavenPublication) {
        from components.java
      }
    }
  }

  repositories {
    mavenLocal()
    mavenCentral()
  }

  dependencies {
    compile 'org.jetbrains:annotations:20.1.0'

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  }

  test {
    useJUnitPlatform()
  }
}
